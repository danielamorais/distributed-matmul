uses server.MatmulController
uses server.Server

const char debugMSG[] = "[@Server]"

component provides server.Server requires io.Output out, network.http.HTTPUtil httpUtil, data.IntUtil iu,
    composition.Adapt adapter, composition.RecursiveLoader loader,
    monitoring.ResponseTime rt, time.Timer timer {
    MatmulController mc
    LoadedComponents matmul = loader.load("matmul/Matmul.o")
    LoadedComponents matmulProxy = loader.load("matmul/Matmul.proxy.o")
    LoadedComponents matmulController = loader.load("server/MatmulController.o")
    int lastResponseTime = 0
    Mutex lock = new Mutex()
    bool adaptiveMode = false

    void Server:initialize(opt int requestedMode) {
        int mode = Server.MODE_LOCAL
        if(isset requestedMode) mode = requestedMode

        if(mode == Server.MODE_PROXY) {
            configureProxyMode()
            adaptiveMode = false
        } else {
            configureLocalMode()
            adaptiveMode = (mode == Server.MODE_ADAPTIVE)
        }
    }

    void Server:adaptRepository(opt bool useProxy) {
        rt.markStartTime()
        if(isset useProxy && useProxy) adapter.adaptRequiredInterface(matmulController.mainComponent, "matmul.Matmul", matmulProxy.mainComponent)
        else adapter.adaptRequiredInterface(matmulController.mainComponent, "matmul.Matmul", matmul.mainComponent)
        rt.markFinishTime()
        rt.clearTime()
    }

    char[] Server:handleRequest(char httpRequestBuf[]) {
        if(adaptiveMode) return handleAdaptiveRequest(httpRequestBuf)
        HTTPMessage msg = httpUtil.readHTTPRequest(httpRequestBuf)
        return process(msg)
    }

    char[] handleAdaptiveRequest(char httpRequestBuf[]) {
        HTTPMessage msg = httpUtil.readHTTPRequest(httpRequestBuf)
        rt.markStartTime()
        char response[] = process(msg)
        rt.markFinishTime()
        if(rt.getResult() - lastResponseTime > 200) {
            mutex(lock) adapter.adaptRequiredInterface(matmulController.mainComponent, "matmul.Matmul", matmulProxy.mainComponent)
        }
        lastResponseTime = rt.getResult()
        rt.clearTime()
        return response
    }
    
    char[] Server:process(HTTPMessage request) {
        Response res = mc.handle(request)
        if (res != null) return sendResponse(request, res)
        else return sendResponse(request, build404(request))
    }

    Response build404(HTTPMessage request) {
        return new Response(
            404,
            "Not Found",
            HTTPUtil.SERVER_NAME,
            0,
            request.mimeType,
            ""
        )
    }

    char[] Server:sendResponse(HTTPMessage request, Response response) {
        return httpUtil.buildHTTPResponse(
            response.code,
            response.status,
            response.serverName,
            response.contentLength,
            response.contentType,
            response.body
        )
    }

    void configureLocalMode() {
        out.println("\n$debugMSG - Server is up and running (local)...")
        matmulController.mainComponent.wire("matmul.Matmul", matmul.mainComponent, "matmul.Matmul")
        mc = new MatmulController() from matmulController.mainComponent
    }

    void configureProxyMode() {
        matmulController.mainComponent.wire("matmul.Matmul", matmulProxy.mainComponent, "matmul.Matmul")
        mc = new MatmulController() from matmulController.mainComponent
        adapter.adaptRequiredInterface(matmulController.mainComponent, "matmul.Matmul", matmulProxy.mainComponent)
        out.println("\n$debugMSG - Server is up and running (proxy)...")
    }
}