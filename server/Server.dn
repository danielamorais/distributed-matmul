uses server.MatmulController

const char debugMSG[] = "[@Server]"
const int SERVER_PORT = 8080

component provides server.Server requires io.Output out, network.http.HTTPUtil httpUtil, data.IntUtil iu,
    composition.Adapt adapter, composition.RecursiveLoader loader,
    monitoring.ResponseTime rt, time.Timer timer {
    bool adaptation = false
    MatmulController mc
    LoadedComponents matmul = loader.load("matmul/Matmul.o")
    LoadedComponents matmulProxy = loader.load("matmul/Matmul.proxy.o")
    LoadedComponents matmulController = loader.load("server/MatmulController.o")
    int lastResponseTime = 0
    Mutex lock = new Mutex()

    void Server:scaleToProxy() {
        matmulController.mainComponent.wire("matmul.Matmul", matmul.mainComponent, "matmul.Matmul")
        mc = new MatmulController() from matmulController.mainComponent
        
        out.println("\n$debugMSG - Server is up and running...")
    }

    void Server:initWithProxy() {
        matmulController.mainComponent.wire("matmul.Matmul", matmulProxy.mainComponent, "matmul.Matmul")
        mc = new MatmulController() from matmulController.mainComponent
        adapter.adaptRequiredInterface(matmulController.mainComponent, "matmul.Matmul", matmulProxy.mainComponent)
        
        out.println("\n$debugMSG - Server is up and running...")
    }

    void Server:init() {
        out.println("\n$debugMSG - Server is up and running...")

        matmulController.mainComponent.wire("matmul.Matmul", matmul.mainComponent, "matmul.Matmul")
        mc = new MatmulController() from matmulController.mainComponent
    }

    void Server:adaptRepository(opt bool useProxy) {
        rt.markStartTime()
        if(isset useProxy && useProxy) adapter.adaptRequiredInterface(matmulController.mainComponent, "matmul.Matmul", matmulProxy.mainComponent)
        else adapter.adaptRequiredInterface(matmulController.mainComponent, "matmul.Matmul", matmul.mainComponent)
        rt.markFinishTime()
        // out.println("[SERVER] - adaptation time: $(iu.makeString(rt.getResult()))")
        rt.clearTime()
    }

    char[] Server:handleRequest(char httpRequestBuf[]) {
        HTTPMessage msg = httpUtil.readHTTPRequest(httpRequestBuf)
        return process(msg)
    }

    char[] handleScaleRequest(char httpRequestBuf[]) {
        HTTPMessage msg = httpUtil.readHTTPRequest(httpRequestBuf)
        rt.markStartTime()
        char response[] = process(msg)
        rt.markFinishTime()
        if(rt.getResult() - lastResponseTime > 200) {
            mutex(lock) adapter.adaptRequiredInterface(matmulController.mainComponent, "matmul.Matmul", matmulProxy.mainComponent)
        }
        lastResponseTime = rt.getResult()
        rt.clearTime()
        return response
    }
    
    char[] Server:process(HTTPMessage request) {
        Response res = null
        res = mc.handle(request)
        if (res != null) return sendResponse(request, res)
        else return sendResponse(request, build404(request))
    }

    Response build404(HTTPMessage request) {
        return new Response(
            404,
            "Not Found",
            HTTPUtil.SERVER_NAME,
            0,
            request.mimeType,
            ""
        )
    }

    char[] Server:sendResponse(HTTPMessage request, Response response) {
        return httpUtil.buildHTTPResponse(
            response.code,
            response.status,
            response.serverName,
            response.contentLength,
            response.contentType,
            response.body
        )
    }

}