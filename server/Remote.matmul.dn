uses Constants
uses network.rpc.RPCUtil
const char debugMSG[] = "[@Remote]"

component provides server.Remote:matmul requires io.Output out, data.IntUtil iu, data.json.JSONEncoder je, 
    data.StringUtil su, network.http.HTTPUtil httpUtil, network.rpc.RPCUtil rpc, matmul.Matmul remoteComponent {
	
	char[] Remote:processHTTPRequest(HTTPMessage request) {
		Request req = parseHTTPRequest(request)
		Response res = process(req)
		return buildHTTPResponse(res)
	}

	Request parseHTTPRequest(HTTPMessage request) {
		// Parse the JSON body which contains the RPC Request
		Request req = je.jsonToData(request.postData, typeof(Request))
		return req
	}

	char[] buildHTTPResponse(Response res) {
		char responseBody[] = je.jsonFromData(res)
		char httpResponse[] = httpUtil.buildHTTPResponse(
			200,
			"OK",
			HTTPUtil.SERVER_NAME,
			responseBody.arrayLength,
			"application/json",
			responseBody
		)
		return httpResponse
	}

	Response process(Request req) {
		char method[] = rpc.getMethodFromMetadata(req.meta)

		if(method == "calcLine") {
			CalcLineParamsFormat paramsData = je.jsonToData(req.content, typeof(CalcLineParamsFormat))
			Line result = remoteComponent.calcLine(remoteComponent.charToLine(paramsData.line),remoteComponent.charToMatrix(paramsData.B))
			return rpc.buildResponseWithData("calcLine", "200", remoteComponent.lineToChar(result))
		}

		if(method == "multiply") {
			MultiplyParamsFormat paramsData = je.jsonToData(req.content, typeof(MultiplyParamsFormat))
			Matrix result = remoteComponent.multiply(remoteComponent.charToMatrix(paramsData.A),remoteComponent.charToMatrix(paramsData.B))
			return rpc.buildResponseWithData("multiply", "200", remoteComponent.matrixToChar(result))
		}

		return rpc.buildResponse(method, "404")
	}

}

