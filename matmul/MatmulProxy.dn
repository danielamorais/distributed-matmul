uses network.rpc.RPCUtil

component provides matmul.Matmul(AdaptEvents) requires data.IntUtil iu, data.StringUtil su, io.Output out,
    network.http.HTTPRPCUtil connection, data.json.JSONEncoder je {
    char remotes[] = new char[]("http://localhost:8081/rpc")
    int addressPointer = 0
    Mutex pointerLock = new Mutex()
    
    Line Matmul:calcLine(Line line, Matrix B) {
        CalcLineParamsFormat params = new CalcLineParamsFormat(lineToChar(line), matrixToChar(B)) // group data
        char requestBody[] = je.jsonFromData(params) // transform grouped data on string
        Request req = new Request(buildMetaForMethod("calcLine"), requestBody)
        Response res = distribute(req) // response.content format - [1, 2, 3]
        return charToLine(res.content)
    }

    Matrix Matmul:multiply(Matrix A, Matrix B) {
        MultiplyParamsFormat params = new MultiplyParamsFormat(matrixToChar(A), matrixToChar(B)) // group data
        char requestBody[] = je.jsonFromData(params) // transform grouped data on string
        Request req = new Request(buildMetaForMethod("multiply"), requestBody)
        Response res = distribute(req) // response.content format - [[1, 2, 3], [4, 5, 6], [7, 8, 9]]
        return charToMatrix(res.content)
    }
   
    char[] Matmul:matrixToChar(Matrix matrix) {
        char result[] = new char[]("[")

        for(int i=0; i < matrix.lines.arrayLength; i++) {
            result = new char[](result, "[")
            int currentLine[] = matrix.lines[i].line

            for(int j=0; j<currentLine.arrayLength; j++) {
                if (j == currentLine.arrayLength - 1) result = new char[](result, iu.makeString(currentLine[j]))
                else result = new char[](result, iu.makeString(currentLine[j]), ",")
            }

            if (i == matrix.lines.arrayLength - 1) result = new char[](result, "]")
            else result = new char[](result, "],")
        }

        result = new char[](result, "]")
        return result
    }

    char[] Matmul:lineToChar(Line line) {
        char result[] = new char[]("[")

        for(int i=0; i<line.line.arrayLength; i++) {
            if (i == line.line.arrayLength - 1) result = new char[](result, iu.makeString(line.line[i]))
            else result = new char[](result, iu.makeString(line.line[i]), ",")
        }

        result = new char[](result, "]")
        return result
    }

    Matrix Matmul:charToMatrix(char matrixChar[]) {
        String linesChar[] = null
        Matrix resultMatrix = new Matrix()
        char acc[] = ""
        bool startRecording = false

        for(int i=1; i < matrixChar.arrayLength - 1; i++) {
            if(matrixChar[i] == "[") startRecording = true
            if(startRecording) acc = new char[](acc, matrixChar[i])
            if(matrixChar[i] == "]") {
                startRecording = false
                linesChar = new String[](linesChar, new String(acc))
                acc = ""
            }
        }

        for(int k=0; k < linesChar.arrayLength; k++) {
            char lineString[] = linesChar[k].string
            resultMatrix.lines = new Line[](resultMatrix.lines, charToLine(lineString))
        }


        return resultMatrix
    }

    Line Matmul:charToLine(char lineString[]) {
        char onlyNumbers[] = su.subString(lineString, 1, lineString.arrayLength - 2)
        String lineStrings[] = su.explode(onlyNumbers, ",")

        int lineIntAcc[] = new int[]()
        for(int j=0; j < lineStrings.arrayLength; j++) {
            lineIntAcc = new int[](lineIntAcc, iu.intFromString(su.trim(lineStrings[j].string)))
        }

        return new Line(lineIntAcc)
    }

    Response distribute(Request req) {
        char url[] = remotes[addressPointer]
        mutex(pointerLock) {
            addressPointer++
            if(addressPointer >= remotes.arrayLength) addressPointer = 0
        }
        return connection.makeHTTPRPC(url, req)
    }

    Metadata[] buildMetaForMethod(char method[]) {
        Metadata metaMethod = new Metadata("method", method)
        return new Metadata[](metaMethod)
    }

    void AdaptEvents:active() {}

	void AdaptEvents:inactive() {}
}
